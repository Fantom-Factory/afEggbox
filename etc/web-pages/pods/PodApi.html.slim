-? using fandoc

-- app.renderLayout(type.qname)

	ol.breadcrumb
		li;	a (href="${podsUrl}")	Pods
		li;	a (href="${podSummaryUrl(pod)}")	${pod.name}
		li;	a (href="${podApiUrl(pod)}")		API
		li.active	${type.name}

	div.row
		div.col-sm-9.api
			h1.podHeading
				span; == DocFlags.toTypeDis(type.flags)
				== type.qname

			// writeTypeInheritance
			pre
				-- indent := 0
				-- type.base.eachr |ref|
					-- if (indent > 0)
						== "\n${Str.spaces(indent*2)}"
					== app.renderDocTypeRefTemplate(ctx, ref, true)
					-- indent++
				-- if (type.base.size > 0) 
					== "\n${Str.spaces(indent*2)}"
				== type.qname
				-- type.mixins.each |ref, i|
					== i==0 ? " : " : ", "
					== app.renderDocTypeRefTemplate(ctx, ref, true)
			
			// writeFacets
			-- if (type.facets.size > 0)
				p.facets.sig
					-- type.facets.each |f|
						== app.renderDocFacetTemplate(ctx, f)
						br
			
			== writeFandoc(type.doc)
	
			-- if (DocFlags.isEnum(type.flags))
				ul
					-- type.slots.each |s|
						-- if (DocFlags.isEnum(s.flags))
							li;	a (href="#${s.name}")	${s.name}
			
			dl
				-- type.slots.each |slot|
					dt#${slot.name}	$slot.name
					dd
						-- if (slotHasSrc(slot))
							p.src
								a (href="${slotSrcUrl}") Source
	
						p.sig; code
							-- slot.facets.each |f|
								== app.renderDocFacetTemplate(ctx, f)
								br
							
							-- if (slot is DocField)
								-- field := (DocField) slot
								== DocFlags.toSlotDis(field.flags) + " "
								== app.renderDocTypeRefTemplate(ctx, field.type, false)
								== " ${field.name}"
								== field.init != null ? " := ${field.init.toXml}" : ""
								== field.setterFlags != null ? " { " + DocFlags.toSlotDis(field.setterFlags) + " set }" : ""
							
							-- else
								-- method := (DocMethod) slot
								-- if (DocFlags.isCtor(method.flags))
									== DocFlags.isStatic(method.flags) ? "static " : ""
									== "new"
								-- else
									== DocFlags.toSlotDis(method.flags) + " "
									== app.renderDocTypeRefTemplate(ctx, method.returns, false)
								== " $method.name("
								-- method.params.each |param, i|
									== i > 0 ? ", " : ""
									== app.renderDocTypeRefTemplate(ctx, param.type, false)
									== " $param.name"
									== param.def != null ? " := $param.def.toXml" : ""
								== ")"
						
						== writeFandoc(slot.doc)
	
		div.col-sm-3.hidden-xs.contents
			h3	Source
			ul.list-unstyled
				-- if (typeHasSrc)
					li;	a (href="${typeSrcUrl}")	View Source
				-- else
					li	Not available
	
			h3	Slots
			ul.list-unstyled
				-- type.slots.each |slot|
					li;	a (href="#${slot.name}")	${slot.name}
